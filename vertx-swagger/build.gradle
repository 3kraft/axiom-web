buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

def projectName = 'Axiom Web Vertx Swagger'

group = 'org.zalando'
version = '0.3.0-SNAPSHOT'
description = 'The vertx swagger integration library creates vertx-web bindings using a swagger definition file.'

apply plugin: 'java'
compileJava {
    sourceCompatibility = '1.8'
}

apply plugin: 'findbugs'
apply plugin: 'maven'
apply plugin: 'signing'

jar {
    manifest {
        attributes(
                "Implementation-Title": "axiom.web.vertx-swagger",
                "Implementation-Version": version,
                "Implementation-Vendor": "Zalando SE"
        )
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    required { !version.endsWith("SNAPSHOT") }
    sign configurations.archives
}

test {
    testLogging {
        showStandardStreams = true
        events = ["standard_out"]
        showExceptions = true
        exceptionFormat = "full"

        debug {
            events = ["started", "skipped", "failed", "standard_out"]
            exceptionFormat = "full"
        }

        error {
            events = ["started", "skipped", "failed", "standard_out"]
            exceptionFormat = "full"
        }
    }
}

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException("Java 8 needed!".stripMargin())
}

repositories {
    mavenCentral()
    maven {
        url = "http://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    compile "io.swagger:swagger-models:1.5.4"
    compile "io.swagger:swagger-parser:1.0.13"
    compile "io.vertx:vertx-web:3.1.0"
    compile "com.netflix.hystrix:hystrix-core:1.4.20"
    compile "org.apache.httpcomponents:httpclient:4.5.1"
    compile "org.slf4j:slf4j-api:1.7.13"
    compile "io.vertx:vertx-dropwizard-metrics:3.1.0"
    testCompile "org.apache.logging.log4j:log4j:2.4.1"
    testCompile "org.apache.logging.log4j:log4j-api:2.4.1"
    testCompile "org.apache.logging.log4j:log4j-core:2.4.1"
    testCompile "org.apache.logging.log4j:log4j-slf4j-impl:2.4.1"
    testCompile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.6.0"
    testCompile "org.assertj:assertj-core:3.2.0"
    testCompile "junit:junit:4.12"
    testCompile "io.swagger:swagger-parser-project:1.0.13"
    testCompile "io.vertx:vertx-unit:3.1.0"
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.9"
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
               name projectName
               packaging 'jar'
               description project.description
               url 'https://github.com/zalando/axiom-web'

               scm {
                   url 'scm:git@git@github.com:zalando/axiom-web.git'
                   connection 'scm:git@git@github.com:zalando/axiom-web.git'
                   developerConnection 'scm:git@git@github.com:zalando/axiom-web.git'
               }

               licenses {
                   license {
                       name 'The MIT License (MIT)'
                       url 'https://opensource.org/licenses/MIT'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'teppel'
                       name 'Tamas Eppel'
                   }
               }
           }
        }
    }
}

defaultTasks 'clean', 'test', 'findbugsMain', 'build'
